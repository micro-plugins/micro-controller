plugins {
    id "java-library"
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.28.3"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}


group = "com.sxtanna.mc.micro"
version = "1.0.0-SNAPSHOT"


repositories {
    mavenCentral()

    maven {
        url = uri("https://jitpack.io")
    }
    maven {
        url = uri("https://repo.aikar.co/content/groups/aikar/")
    }
    maven {
        url = uri("https://papermc.io/repo/repository/maven-public/")
    }
}

dependencies {
    compileOnly("org.jetbrains:annotations:23.0.0")

    compileOnly("io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT")

    implementation("ch.jalu:configme:1.3.0")
    implementation("co.aikar:acf-paper:0.5.0-SNAPSHOT")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.1")
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

test {
    useJUnitPlatform()
}

configurations {
    testImplementation.extendsFrom(compileOnly)
}

processResources {
    filesMatching("*.yml") {
        expand(["project_version": rootProject.version,
                "jenkins_version": System.getenv("BUILD_NUMBER")])
    }
}

shadowJar {
    archiveClassifier.set("")

    relocate 'ch.jalu', 'com.sxtanna.mc.micro.libs'

    relocate 'co.aikar.commands', 'com.sxtanna.mc.micro.libs.commands'
    relocate 'co.aikar.locales', 'com.sxtanna.mc.micro.libs.locales'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-parameters"]

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.mavenJava)
}


artifactory {
    contextUrl = "https://repo.sxtanna.com/artifactory"

    publish {
        repository {
            repoKey = "micro-plugins"

            username = project.hasProperty("sxtannaArtifactoryUser") ? sxtannaArtifactoryUser : System.getenv("sxtannaArtifactoryUser")
            password = project.hasProperty("sxtannaArtifactoryAuth") ? sxtannaArtifactoryAuth : System.getenv("sxtannaArtifactoryAuth")
        }

        defaults {
            publishArtifacts = true
        }
    }
}